'Konstanten
Public Const Faecherzahl As Integer = 4
Public Const StrWahlen As String = "Wahlen"
Public Const StrWahlopt As String = "Wahlmoeglichkeiten"
Public Const StrZuteilung As String = "Zuteilung"



Sub FachVerteilung()
    '------------------------------------------------------
    'Pruefen ob Arbeitsmappe korrekt aufgebaut
    '------------------------------------------------------
    Dim Response As Integer
    Dim Msg, Style, Title
    
    'Wahloptionen liegen in "Wahlmoeglichkeiten"
    ThisWorkbook.Sheets(1).Activate
    Title = "Tabelle kontrollieren"
    Msg = "Die Wahlmoeglichkeiten wurden im Tabellenblatt '" & StrWahlopt & "' abgelegt." & vbNewLine & "Spalte A ist die Kennziffer (aufsteigend sortiert)," & vbNewLine & "Es gibt keine Titel, Kennziffer 1 steht in Zeile 1." & vbNewLine & "Spalte B gibt das Fach an und Spalte C die Kursgroesse." & vbNewLine & "Korrekt?"
    Style = vbOKCancel 'Nur die Schaltflächen OK und Abbrechen anzeigen.
    Response = MsgBox(Msg, Style, Title)
    If Response = vbCancel Then
        'Abbruch des Subs bei falschem Format -> Abbrechen-Button geklickt
        Exit Sub
    End If
    
    'Faecherzahl ist die richtige Konstante
    If Faecherzahl <> Faecherzaehlen Then
        Title = "Konstante anpassen"
        Msg = "Die Konstante fuer die Anzahl der Wahlmoeglichkeiten ist nicht aktuell. Bitte anpassen!" & vbNewLine & "Im Makro, zweite Zeile 'Faecherzahl' sollte " & Faecherzaehlen & " sein."
        Style = vbOKOnly 'Nur die Schaltflächen OK anzeigen.
        Response = MsgBox(Msg, Style, Title)
        Exit Sub
    End If
    
    'Wahlen der Schueler:innen liegen in "Wahlen"
    ThisWorkbook.Sheets(2).Activate
    Title = "Tabelle kontrollieren"
    Msg = "Die Wahlen der Schueler:innen wurden im Tabellenblatt '" & StrWahlen & "' abgelegt." & vbNewLine & "Die Namen stehen in Spalte A und B," & vbNewLine & "die Wuensche in Spalten C, D und E." & vbNewLine & "Die Schueler:innen wurden absteigend nach Zettelabgabe sortiert." & vbNewLine & "Korrekt?"
    Style = vbOKCancel 'Nur die Schaltflächen OK und Abbrechen anzeigen.
    Response = MsgBox(Msg, Style, Title)
    If Response = vbCancel Then
        'Abbruch des Subs bei falschem Format -> Abbrechen-Button geklickt
        Exit Sub
    End If
    
    '------------------------------------------------------
   
   
    '------------------------------------------------------
    'SetUp
    'Anlegen des Tabellenblatts zur Verteilung und der Arrays mit den Wahlen und freien Plaetzen
    '------------------------------------------------------
    Dim i As Integer
    Dim count As Integer
    Dim Student As Integer
    Dim zutSpalte As Integer
    Dim ZuteilungsSheet As Worksheet
    
    '1. Zuteilung-Tabellenblatt anlegen und Schueler:innenliste mit Wuenschen kopieren
    With ThisWorkbook
        count = .Sheets.count
        Worksheets(StrWahlen).Copy After:=.Sheets(count)
        'Tabelle umbennnen. Um bei jeder neuen Verteilung einen individ. Namen anzugeben, wird einfach gezaehlt, minus die zwei festen Tabellen Moeglichkeiten und Wahlen
        ActiveSheet.Name = StrZuteilung & count - 2
        Set ZuteilungsSheet = ActiveSheet
    End With
    
    'Zuteilung in Spalte G schreiben
    zutSpalte = 7
    ZuteilungsSheet.Cells(1, zutSpalte) = "Zuteilung"
    ZuteilungsSheet.Cells(1, zutSpalte + 1) = "Fachname"
    
    '2. Zaehlen, wie oft welches Fach gewaehlt wurde und in 3 Arrays ablegen, Index ist Fachkennziffer
    Dim Erstwunsch(Faecherzahl) As Integer
    Dim Zweitwunsch(Faecherzahl) As Integer
    Dim Drittwunsch(Faecherzahl) As Integer
    Dim LastStudent As Integer
    Dim BelowStudents As Integer
    LastStudent = Schuelerzaehlen
    BelowStudents = LastStudent + 5
    'Hinweistabelle vorbereiten:
    ZuteilungsSheet.Cells(BelowStudents, 1) = "Fach"
    ZuteilungsSheet.Cells(BelowStudents, 2) = "# Erstwunsch"
    ZuteilungsSheet.Cells(BelowStudents, 3) = "# Zweitwunsch"
    ZuteilungsSheet.Cells(BelowStudents, 4) = "# Drittwunsch"
    
    'Zaehlen
    For i = 1 To Faecherzahl
        With ThisWorkbook.Sheets(StrWahlen)
            Erstwunsch(i) = WorksheetFunction.CountIf(.Range(.Cells(2, 3), .Cells(LastStudent, 3)), i)   'C2:C LastStudent
            Zweitwunsch(i) = WorksheetFunction.CountIf(.Range(.Cells(2, 4), .Cells(LastStudent, 4)), i)  'D2:D LastStudent
            Drittwunsch(i) = WorksheetFunction.CountIf(.Range(.Cells(2, 5), .Cells(LastStudent, 5)), i)  'E2:E LastStudent
        End With
        'Als Hinweis ablegen:
        ZuteilungsSheet.Cells(BelowStudents + i, 1) = GetSubjectname(i)
        ZuteilungsSheet.Cells(BelowStudents + i, 2) = Erstwunsch(i)
        ZuteilungsSheet.Cells(BelowStudents + i, 3) = Zweitwunsch(i)
        ZuteilungsSheet.Cells(BelowStudents + i, 4) = Drittwunsch(i)
        
    Next i

        
    '3. Array anlegen, mit verfuegbaren Plaetzen je Fach, Index ist Fachkennziffer
    Dim FreiePlaetze(Faecherzahl) As Integer
    For i = 1 To Faecherzahl
        FreiePlaetze(i) = ThisWorkbook.Sheets(StrWahlopt).Cells(i, 3).Value
    Next i
    '------------------------------------------------------
        
    
    '------------------------------------------------------
    'Verteilung
    'Annahme: Schuelerinnen die frueh abgeben, stehen oben in der Liste
    'Je weiter oben jemand steht, desto eher bekommen sie ihren Erstwunsch
    '------------------------------------------------------
    Dim wunsch1 As Integer
    Dim wunsch2 As Integer
    Dim ohnePlatz As Integer
    ohnePlatz = 0
    
    'Schueler:innen von oben nach unten durchgehen
    For Student = 2 To LastStudent
        'Wuensche aus Tabelle auslesen (Erstwunsch in Zpalte C, usw.)
        wunsch1 = GetWish(Student, 1)
        wunsch2 = GetWish(Student, 2)
        
        '4.Erstwunsch zuteilen, bis Kurs voll
        If FreiePlaetze(wunsch1) > 0 Then
            'Zuteilung in Spalte G schreiben
            ZuteilungsSheet.Cells(Student, zutSpalte) = wunsch1 'Kennziffer
            ZuteilungsSheet.Cells(Student, zutSpalte + 1) = GetSubjectname(wunsch1) 'Fachname
            'Freie Plaetze verringern
            FreiePlaetze(wunsch1) = FreiePlaetze(wunsch1) - 1
            
        '5. Wenn Erstwunsch schon voll, Zweitwunsch vergeben
        ElseIf FreiePlaetze(wunsch2) > 0 Then
            'Zuteilung in Spalte G schreiben
            ZuteilungsSheet.Cells(Student, zutSpalte) = wunsch2 'Kennziffer
            ZuteilungsSheet.Cells(Student, zutSpalte + 1) = GetSubjectname(wunsch2) 'Fachname
            'Freie Plaetze verringern
            FreiePlaetze(wunsch2) = FreiePlaetze(wunsch2) - 1
        End If
    Next Student
    
    '------------------------------------------------------

    Dim wunsch1x As Integer
    Dim wunsch2x As Integer
    Dim wunsch3x As Integer
    Dim zuteilungy As Integer
    Dim wunsch1y As Integer
    Dim wunsch2y As Integer
'#
    i = 1
    '6. Bei allen verbleibenden gucken, ob man Tauschen kann
    With ZuteilungsSheet
        'von oben nach unten Schueler:in x waehlen, die noch kein Fach haben
        For x = 2 To LastStudent
            If IsEmpty(.Cells(x, zutSpalte).Value) Then 'Zuteilung wuerde in Cells(x, zutSpalte) stehen
                'Wuensche von x bestimmen
                wunsch1x = GetWish(x, 1)
                wunsch2x = GetWish(x, 2)
                wunsch3x = GetWish(x, 3)
            
                '6.a von unten nach oben: Schueler:in y finden,
                For y = LastStudent To 2 Step -1
                    'mit Erstwunsch im Fach, das x als Zweitwunsch hat, und mit Zweitwunsch von y noch nicht voll
                    zuteilungy = ConvertToInt(.Cells(y, zutSpalte))
                    wunsch1y = GetWish(y, 1)
                    wunsch2y = GetWish(y, 2)
                    If zuteilungy = wunsch1y And wunsch2x = wunsch1y And FreiePlaetze(wunsch2y) > 0 Then
                        'tauschen
                        'x den Zweitwunsch geben (erstwunsch von y)
                        .Cells(x, zutSpalte) = wunsch2x 'Kennziffer
                        .Cells(x, zutSpalte + 1) = GetSubjectname(wunsch2x) 'Fachname
                        'y den Zweitwunsch geben
                        .Cells(y, zutSpalte) = wunsch2y 'Kennziffer
                        .Cells(y, zutSpalte + 1) = GetSubjectname(wunsch2y) 'Fachname
                        FreiePlaetze(wunsch2y) = FreiePlaetze(wunsch2y) - 1
'#
                        .Cells(BelowStudents + 6 + i, 1) = x & " mit " & y & " getauscht fuer Zweitwunsch"
                        i = i + 1
                        'Bei Erfolg naechste:n Schueler:in x ohne Zuteilung finden
                        GoTo NextStudent
                    End If
                Next y
                
                '6.b wenn es keine:n y gibt, dann bekommt x Drittwunsch, wenn noch Platz da ist
                'ggf. diesen Block 6.b und den naechsten 6.c tauschen
                If FreiePlaetze(wunsch3x) > 0 Then
                    .Cells(x, zutSpalte) = wunsch3x 'Kennziffer
                    .Cells(x, zutSpalte + 1) = GetSubjectname(wunsch3x) 'Fachname
                    FreiePlaetze(wunsch3x) = FreiePlaetze(wunsch3x) - 1
                    
                    'Bei Erfolg naechste:n Schueler:in x ohne Zuteilung finden
                    GoTo NextStudent
                End If
                
                '6.c wenn in Drittwunsch nichts mehr frei ist fuer x, dann ein neues y finden, um Erstwunsch zu tauschen
                'von unten nach oben: Schueler:in y finden,
                For y = LastStudent To 2 Step -1
                    'mit Erstwunsch im Fach, das x als Erstwunsch hat, und mit Zweitwunsch von y noch nicht voll
                    zuteilungy = ConvertToInt(.Cells(y, zutSpalte))
                    wunsch1y = GetWish(y, 1)
                    wunsch2y = GetWish(y, 2)
                    If zuteilungy = wunsch1y And wunsch1x = wunsch1y And FreiePlaetze(wunsch2y) > 0 Then
                        'tauschen
                        'x den Erstwunsch geben (erstwunsch von y)
                        .Cells(x, zutSpalte) = wunsch1x 'Kennziffer
                        .Cells(x, zutSpalte + 1) = GetSubjectname(wunsch1x) 'Fachname
                        'y den Zweitwunsch geben
                        .Cells(y, zutSpalte) = wunsch2y 'Kennziffer
                        .Cells(y, zutSpalte + 1) = GetSubjectname(wunsch2y) 'Fachname
                        FreiePlaetze(wunsch2y) = FreiePlaetze(wunsch2y) - 1
'#
                        .Cells(BelowStudents + 6 + i, 1) = x & " mit " & y & " getauscht fuer Erstwunsch"
                        i = i + 1
                        'Bei Erfolg naechste:n Schueler:in x ohne Zuteilung finden
                        GoTo NextStudent
                    End If
                Next y
                
                '7.wenn kein Platz fuer x in Drittwunsch ist: Verteilung auslassen und Zelle farbig hinterlegen
                .Cells(x, zutSpalte).Interior.ColorIndex = 39 '39: violett, 3: knallrot, 6: gelb, 33: hellblau
                ohnePlatz = ohnePlatz + 1
            End If 'isEmpty
            
'Sprungmarke um nach Tausch Schleife zu durchbrechen
NextStudent:
        Next x
    End With
    
    '------------------------------------------------------

        
    '8. uebrig gebliebene Plaetze in Hinweistabelle vermerken
    ZuteilungsSheet.Cells(BelowStudents, 7) = "Verbleibende Plaetze"
    For i = 1 To Faecherzahl
        ZuteilungsSheet.Cells(BelowStudents + i, 7) = FreiePlaetze(i)
    Next i
    
    ZuteilungsSheet.Cells(BelowStudents, 8) = "Schueler:innen ohne Platz"
    ZuteilungsSheet.Cells(BelowStudents + 1, 8) = ohnePlatz
    
    '9. Obere Zelle auswaehlen fuer Uebersicht
    ZuteilungsSheet.Cells(1, 1).Activate
        
End Sub

Function Faecherzaehlen() As Integer
    Faecherzaehlen = ThisWorkbook.Sheets(StrWahlopt).Cells(Rows.count, 1).End(xlUp).Row
End Function

Function Schuelerzaehlen() As Integer
    Schuelerzaehlen = ThisWorkbook.Sheets(StrWahlen).Cells(Rows.count, 1).End(xlUp).Row
End Function

Function ConvertToInt(Cell As Range) As Integer
   On Error GoTo NOT_AN_INTEGER
   ConvertToInt = CInt(Cell.Value)
   Exit Function
NOT_AN_INTEGER:
   ConvertToInteger = 0
End Function

Function GetWish(Stud As Variant, Prio As Integer) As Integer
    'Spalten: 1.Wunsch: C->3 = 2+Prio=1, 2.Wunsch: D->4 = 2+Prio=2, 3.Wunsch: E->5 = 2+Prio=3
    GetWish = ConvertToInt(ThisWorkbook.Sheets(StrWahlen).Cells(Stud, 2 + Prio))
End Function

Function GetSubjectname(Subjectnumber As Variant) As String
    GetSubjectname = ThisWorkbook.Sheets(StrWahlopt).Cells(Subjectnumber, 2).Value
End Function
